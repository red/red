Red [
	Title:	 "Red GUI Console Settings"
	Author:	 "Qingtian Xie"
	File:	 %settings.red
	Tabs:	 4
	Rights:  "Copyright (C) 2014-2018 Red Foundation. All rights reserved."
	License: {
		Distributed under the Boost Software License, Version 1.0.
		See https://github.com/red/red/blob/master/BSL-License.txt
	}
]

fstk-logo: load/as 64#{iVBORw0KGgoAAAANSUhEUgAAAD4AAAA/CAIAAAA3/+y2AAAACXBIWXMAABJ
	0AAASdAHeZh94AAAGr0lEQVR4nNVaTW8kVxU9975XVf0xthNDBMoi4m8EwTZZsEAiG9jwM9jzE/gB
	SBFIMFKEhIDFwIY9e1ggNhERYqTIMx67XR/v3XtYtO3Y3dXVXZXBnjlqtVpVr947dd+p+9UlH373e
	4nEWwUBCpH43ix+/12JSp/Ev1SI6kQKhUJl9EXC5yk++7yOp/Pih9/k6cx8/CwASkN5EiZcCAAqYb
	mgqriPuEj1n5fy7N9dhAhyvnqZ5KiQ8exDys1L02WUoDh83wQUSGJ+VeuiGrVuJZ6tAnC718yX2fM
	U0TDTV5lOjN82unvdThPrDXUROvJVpj04e3Ov2wmL3nnCVOBIF2maDZjcLxPNp7G3y2bsuvedgwpE
	3hblbPm1t0c5fS75AZRD9LojmttFfeC6sf+wCoh8mcMiindw23W9dKmfWy04LofX7iVIc6ZaFwvMn
	yjzwOU7qAMQoWX4Yvnhx2Ex3zUq7I4mUqrGiYHWXpzb53/TYkh2u6kDMC+X7xx/9JPw7Q+mMZiM/M
	W/Vr94xtPTgWg1aBUVE9JGROnXBclXMPdVy927um9DBRN8xesC3bzuduUXe6jTycdKiWX9vLmt+qP
	VHuoiIo9odgBfRatN5bzRgrkFzbeV8wYL5j62lfMWCOYWG8oZ9OuAxIjJ9dvXAKsjQNYVyb3j7l43
	wAxABGl1x5whWxSz5y++XP35M1kud61RFCKqgBAicEAIiJBUFXMGESPDxikRc0SNBaRvSzWmF2dIB
	G37dEM6z0U0Mns+u4JQi2LbB1lzdvabT7E7LlQBYVmpqruoOiAkVGmmIWT3QjW5FyK2dUrjLGgVNQ
	bpe5y0qNCXe6u7BSc9AqCHlFKIEJVN9gIpq4Gi0+EwDWURVODEjUMKAUDUACCsvzdPxXWCrRIL6Vt
	g15oUEArYtUjc2a6yOyd0Fzx7t+rolN7dvwuRjQ+duUkkCNn4DEwT4bj7mLqzq62ahw3bs2sHBONw
	AA54bsqqRCgGlgxVse1qmT3T4nyPw7gLu76BuzyMzZXNlvGavVk4OTn+5McDj+ldDiIaithvegnp/
	FXzp6cqvu2RaWYNwuxQ9mvBbI6ms13lchFEBZ3F46PlR5/E979z4KQDKM6eX/7+aaGQvo6TZ2PDWM
	V+n3Mfa8H0+Oy1cuCEws0ndvS2wPoKGNIws+dmZzl2F+tB/eFmrZyHzwBoZs1QUXc9DIqBRIDOZmX
	ZsNtN/V/g2XKTMGi2nYK5M42zSXzwImmvcoYE8+gYVs4ewTw6PFuuU2/KfYBgHhs0z21Pn+cgwWiQ
	7YRyGmS+AHqzlUFkWrupnLVgBgNYDN3lxfkfn+rx8c36okF1fJ4jAru4LIJLGO2xPBnIcCdabeYwP
	Qgxna+e//pXuPUyTg1azkIsZN3MCxjhgIony2Je9OSne9lnB+w2U+jJYTZBACLFbONYosQyCgCnjq
	FuRjT5mv3IgOfZ0CJUEbtymINmcdaXuVyEoKMbBpadq65clBJGF72ezOmxiuGuYER66jsANDI1sM0
	AYUDbynwZZb4YbT9nalIxjxqEHEk/MzJxUYlIpDPVVphZ2Nx6WVnxwfvv/uBj3dXpreuL3/0WXatx
	KE3vIdCaNVYdFfurky24W7YkEqIbVq/aIiUpig3zed3OT99750c/jd/4Vu8szPV/f/lp0VwVx0ej2
	zVimahmQXU4YdmE0Sy4+41KUgadGybY+3emr1akpC4Q1DDOfqpCZ0pObq576AwAqCFRm4Rps7ixfk
	USY9kDyImpIzCl2aMAxA305Khb+JbtD4E7V+fmNoV9SmwbJ0ezvzc8E03HbeXshQjoqC/NfbRyAOT
	MtvWxVtu800yZphwRuOHqAZXTM/BWOWOXFwEfUDn9o9bKSQfVuPfwkMrZeYOZkoxSVBJ2vu6iy5Pt
	93geTDlDOYzH+OLv/4g//1mcV70DUt15dpbVBsFb5SyOggbxkf/Zp0TSq5mqDvTdBqkzxPr5i/989
	hcRFDvsF0+Oe9P3W+XMj3QC+5yJ1stSRHrbwPuoixNS5EURBKGEaM8sA6K8Vc7yRKawTxSgrGSX7Q
	96mI2ouynR6uv7nKbe6XMO9aKPFa3MdvqcEcH3TYtW4/KGyXnO61TOzdWjE7bHV87NkSlNlteiHJm
	knLb76sWuCACqRWSQcXG/NZlFrWRscQQAdmFyFGUZkMc1Yz2DmcXcRYr40vyv8ydSzWxSrbIM176e
	kHVr65AfAsBFOh1bVQMIhotz0lL8skt/YBSd+l7ujdVIEeGBPwDQRCa9kghAakbk/wFTSfh53Lxjk
	wAAAABJRU5ErkJggg==
} 'png

set-background: function [color [tuple!]][
	console/color: color
	system/view/platform/redraw console
]

set-font-color: func [color [tuple!] /local clr][
	console/font/color: color
	terminal/foreground: color
	if 3 = length? color [clr: color or 0.0.0.1]
	caret/color: clr
	caret-clr: clr
	system/view/platform/redraw console
]

display-about: function [][
	;-- cloak URLs to avoid false positives from bad AV software
	red-lang: to-string debase "aHR0cHM6Ly93d3cucmVkLWxhbmcub3Jn"
	github:   to-string debase "aHR0cHM6Ly9naXRodWIuY29tL3JlZC9yZWQ="

	lay: layout/tight [
		title "About"
		size 360x330
		backdrop 58.58.60

		style text:  text 360 center 58.58.60
		style txt:   text font-color white
		style small: txt  font [size: 9 color: white]
		style link:  text cursor 'hand all-over
			on-down [browse to-url face/text]
			on-over [face/font/style: either event/away? [none]['underline]]

		below
		pad 0x15
		txt bold "Red Programming Language" font [size: 15 color: white]
		ver: txt font [size: 9 color: white]
		at 153x86 image fstk-logo
		at 0x160 small 360x20 "Copyright 2011-2024 - Red Foundation"
		at 0x180 small 360x20 "and contributors."
		at 0x230 link red-lang font-size 10 font-color white
		at 0x260 link github   font-size 10 font-color white
		at 154x300 button "Close" [unview win/selected: console]
		do [ver/text: form reduce [
				"Build" system/version #"-" any [
					all [system/build/git system/build/git/date]
					system/build/date
				]
		]]
	]
	center-face/with lay win
	view/flags lay [modal no-title]
]

set-dark-mode: func [
	dark?	[logic!]
][
	foreach face gui-console-ctx/win/parent/pane [
		system/view/platform/set-dark-mode face dark?
	]	
	system/view/platform/set-dark-mode win dark?
	system/view/platform/set-dark-mode console dark?
]

show-cfg-dialog: function [][
	lay: layout [
		title "Settings"
		style bbox: base 20x20 draw [pen gray box 0x0 19x19] on-down [
			set-background cfg-backcolor/data: face/color
		]
		style fbox: bbox on-down [
			set-font-color cfg-forecolor/data: face/color
		]
		style hex-field: field 90 center font [name: font/name]

		group-box "Background color" [
			bbox #000000 bbox #002b36 bbox #073642 bbox #293955
			bbox #eee8d5 bbox #fdf6e3 bbox #ffffff
			cfg-backcolor: hex-field
		]
		return

		group-box "Font color" [
			fbox #b98000 fbox #cb4b16 fbox #dc322f fbox #d33682
			fbox #6c71c4 fbox #268bd2 fbox #2aa198
			cfg-forecolor: hex-field
			return
			fbox #859900 fbox #82bb82 fbox #000000 fbox #657b83
			fbox #839496 fbox #93a1a1 fbox #ffffff
		]
		return

		mouse-mode: check "Mouse Copy&&Paste" on-create [
			face/data: cfg/mouse-paste? = 'true
		]
		pad -3x0 text "Buffer Lines:" 80 middle
		pad -17x0 cfg-buffers: hex-field right return
		check "Dark Mode" [
			cfg/dark-mode?: to-word face/data
			set-dark-mode face/data
		] on-create [
			unless system/view/platform/support-dark-mode? [
				face/enabled?: no
				exit
			]
			face/data: cfg/dark-mode? = 'true
		]
		return

		pad 90x20
		button "OK" [
			if cfg/buffer-lines <> cfg-buffers/data [
				cfg/buffer-lines: cfg-buffers/data
				terminal/max-lines: cfg/buffer-lines
			]
			set-font-color cfg/font-color: cfg-forecolor/data
			set-background cfg/background: cfg-backcolor/data
			cfg/mouse-paste?: to-word mouse-mode/data
			toggle-mouse-mode
			unview
		]
		button "Cancel" [unview]
	]
	cfg-buffers/data:	cfg/buffer-lines
	cfg-forecolor/data:	cfg/font-color
	cfg-backcolor/data:	cfg/background
	center-face/with lay win
	view/flags lay [modal]
]

apply-cfg: function [][
	screen: get-current-screen
	win/size: cfg/win-size
	either within? cfg/win-pos screen/offset screen/size [
		win/offset: cfg/win-pos
	][
		center-face/with win screen
	]
	font: make font! [
		name:  cfg/font-name
		size:  cfg/font-size
		color: cfg/font-color
	]
	gui-console-ctx/font: font
	console/font: font
	ft: copy font
	ft/color: white
	tips/font: ft
	terminal/update-cfg font cfg
	set-font-color cfg/font-color
	system/console/history: cfg/history
	terminal/history: cfg/history
	if cfg/dark-mode? = 'true [set-dark-mode yes]
]

save-cfg: function [][
	unless exists? cfg-dir [make-dir/deep cfg-dir]
	offset: win/offset					;-- offset could be negative in some cases
	if offset/x < 0 [offset/x: 0]
	if offset/y < 0 [offset/y: 0]
	cfg/win-pos:  offset
	cfg/win-size: win/size
	cfg/font-name: console/font/name
	cfg/font-size: console/font/size
	clear skip cfg/history 100
	save/header cfg-path cfg [Purpose: "Red Console Configuration File"]
]

check-cfg: function [gui-default][
	iter: gui-default
	while [not tail? iter][
		either f: find cfg iter/1 [
			if (type? f/2) <> type? iter/2 [
				f/2: iter/2
			]
		][
			repend cfg [iter/1 iter/2]
		]
		iter: skip iter 2
	]
]

load-cfg: func [/local cfg-content gui-default][
	system/view/auto-sync?: no
	cfg-dir: append copy system/options/cache
			#either config/OS = 'Windows [%Red-Console/][%.Red-Console/]

	unless exists? cfg-dir [make-dir/deep cfg-dir]
	cfg-path: append copy cfg-dir %console-cfg.red

	gui-default: compose [
		win-pos:	  200x200
		win-size:	  640x480

		font-name:	  (font/name)
		font-size:	  11
		font-color:	  0.0.0
		background:	  252.252.252
		mouse-paste?: false
		menu-bar?:	  true
		dark-mode?:   no
	]
	gui-default/win-pos: (200, 200)

	either all [
		exists? cfg-path
		attempt [select cfg-content: load cfg-path 'Red]
	][
		cfg: skip cfg-content 2
		check-cfg gui-default
	][
		cfg: gui-default
	]
	unless find cfg 'buffer-lines [
		append cfg [buffer-lines: 10000]
	]
	unless find cfg 'history [
		append cfg [history: []]
	]

	toggle-mouse-mode
	toggle-menu-bar
]